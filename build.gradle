plugins {
    id 'java-library'
    id "com.palantir.git-version" version "3.0.0"
}

def versionLabel(gitInfo) {
    def branch = gitInfo.branchName // all branches are snapshots, only tags get released
    def tag = "test" // gitInfo.lastTag
    // tag is returned as is. Branch may need cleanup
    return branch == null ? tag : "99." + branch.replace("/","-") + "-SNAPSHOT"
}

group = 'mil.army.usace.hec'
version = versionLabel(versionDetails())

repositories {
    maven {
        url = uri("https://www.hec.usace.army.mil/nexus/repository/maven-public/")
    }
    mavenCentral()
}

configurations {
    javaHeclib
}

dependencies {
    javaHeclib (libs.heclib)
    compileOnly (libs.jetbrainsAnnotations)
    implementation(libs.hec.monolith) {
        exclude group: "javax.media", module: "jai_core"
        exclude group: "javax.media", module: "jai_coded"
    }
    implementation(libs.sqliteJdbc)
    implementation (libs.jacksonDatabind)
    testImplementation platform(libs.junitBom)
    testImplementation (libs.junitJupiter)
    testRuntimeOnly (libs.junitPlatformLauncher)
    testCompileOnly (libs.jetbrainsAnnotations)
    testRuntimeOnly (libs.org.slf4j) // needed for sqlite
}

test {
    useJUnitPlatform()
}

tasks.register("UnpackJavaHeclib", Copy) {
    from {
        configurations.javaHeclib.collect { zipTree(it) }
    }
    into layout.buildDirectory.dir("native")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named("test") {
    dependsOn("UnpackJavaHeclib")
    systemProperty "java.library.path", layout.buildDirectory.dir("native").get().asFile.absolutePath
}
